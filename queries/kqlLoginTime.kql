WVDConnections
| where State == "Connected"
| extend HostPool = tostring(split(_ResourceId, '/')[-1])
| project CorrelationId, HostPool, UserName, ConnectTime = TimeGenerated
| join kind=inner (
    WVDConnections
    | where State == "Started"
    | project CorrelationId, StartTime = TimeGenerated
) on CorrelationId
| extend LoginDurationSec = datetime_diff('second', ConnectTime, StartTime)
| where LoginDurationSec >= 0 and LoginDurationSec < 600
| summarize AvgLoginSec = round(avg(LoginDurationSec), 1), P50LoginSec = round(percentile(LoginDurationSec, 50), 1), P95LoginSec = round(percentile(LoginDurationSec, 95), 1), MaxLoginSec = max(LoginDurationSec), TotalConnections = count() by HostPool
